plugins {
   id 'net.nemerosa.versioning' version '2.6.0'
}
apply plugin: 'base'


group = 'com.logicartisan'
archivesBaseName = 'intrepid'

version = versioning.info.display
println "Version: ${version}"
println "Full version info: ${versioning.info}"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

	group = project.rootProject.group
	version = project.rootProject.version

	ext.sanitizedName = project.name.replace( '/', '-' )


    repositories {
        mavenCentral()
    }



	test {
		systemProperty 'intrepid.lease.duration', '2000'
		systemProperty 'intrepid.lease.prune_interval', '1000'
		systemProperty 'intrepid.local_call_handler.initial_reservation', '10000'
		systemProperty 'intrepid.req_invoke_ack_rate_sec', '1'

		ignoreFailures true
	}



    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        baseName = "intrepid-${sanitizedName}"
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        baseName = "intrepid-${sanitizedName}"
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }


    signing {
        sign configurations.archives
    }


    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
    tasks.withType(Javadoc).all { enabled = false }     // opt-in

    jar {
        println "Group: ${project.group}   Name: ${project.name}   Sanitized: ${sanitizedName}"
        baseName = "intrepid-${sanitizedName}"
    }



    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
	                artifactId "intrepid-${sanitizedName}"

	                name "Intrepid/${sanitizedName}"
                    packaging 'jar'
                    url 'https://bitbucket.org/robeden/intrepid'

                    scm {
                        connection 'scm:git:https://bitbucket.org/robeden/intrepid.git'
                        developerConnection 'scm:git:https://bitbucket.org/robeden/intrepid.git'
                        url 'https://bitbucket.org/robeden/intrepid/src'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'robeden'
                            name 'Rob Eden'
                            email 'robeden1@gmail.com'
                        }
                    }
                }
            }
        }
    }
    uploadArchives.dependsOn sourcesJar
    uploadArchives.dependsOn javadocJar
}